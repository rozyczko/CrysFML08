!!----
!!----
!!----
!!
SubModule (CFML_gSpaceGroups) SPG_Check_Gener
   implicit none
   Contains

   !!----
   !!---- CHECK_GENER
   !!----
   !!---- 20/04/19
   !!
   Module Subroutine Check_Gener(Gen_in, Gen_out)
      !---- Arguments ----!
      character(len=*), dimension(:),              intent(in)  :: gen_in
      character(len=*), dimension(:), allocatable, intent(out) :: gen_out

      !---- Local variables ----!
      logical                                          :: init, newgen
      integer                                          :: i,j,d,d_in,ngen_in,ngen,nop,nop_in,invt
      character(len=40), dimension(size(gen_in))       :: genAux
      type(Symm_Oper_Type), dimension(0:512)           :: Op
      type(rational),       dimension(:,:),allocatable :: Mat

      !> Init
      call clear_error()

      ngen_in   = size(gen_in)
      ngen      = 0
      nop       = 0  ! number of different operations generated by combining generators
      init      = .false.

      do i=1, ngen_in
         if (len_trim(gen_in(i)) == 0) cycle
         if (.not. init) then
            d=Get_Dimension_SymmOp(gen_in(i))
            allocate(Mat(d,d))
            do j=0, size(Op)-1
               call allocate_OP(d, Op(j))
            end do
            init = .true.
         else
            d_in=Get_Dimension_SymmOp(gen_in(i))
            if (d /= d_in) then
               Err_CFML%Ierr = 1
               Err_CFML%Msg  = "Check_Gener@SPACEG: Generators with different dimensions."
               return
            end if
         end if

         if (d < 4) then
            Err_CFML%Ierr = 1
            Err_CFML%Msg  = "Check_Gener@SPACEG: Dimension must be at least 4 (3+1)"
            return
         end if

         call Get_Mat_From_Symb(gen_in(i), Mat, invt)
         if (Err_CFML%Ierr /= 0) return

         call reduced_translation(Mat)

         !> Do not consider the identity operation
         if (Rational_Equal(Mat,Op(0)%Mat) .and. invt == 1) cycle

         newgen = .true.
         !> Check if the generator has already been generated
         do j=1, nop
            if (Op(j)%time_inv == invt .and. Rational_Equal(Op(j)%Mat,Mat)) then
               newgen = .false.
               exit
            end if
         end do

         if (newgen) then
            ngen              = ngen + 1
            nop               = nop  + 1
            Op(nop)%Mat       = Mat
            Op(nop)%time_inv  = invt
            genAux(ngen)      = gen_in(i)
            nop_in            = nop
         end if
      end do
      if (ngen > 0) then
         allocate(gen_out(ngen))
         gen_out(:) = genAux(1:ngen)
      end if

   End Subroutine Check_Gener

End SubModule SPG_Check_Gener

