!!----
!!---- Program: Test Magnetic_Hall
!!----
!!---- JGP May2019
!!
Program Magnetic_Hall
   !---- Use Modules ----!
   Use CFML_Globaldeps, only: cp, err_CFML,clear_error
   Use CFML_Strings,    only: pack_string
   Use CFML_Symmetry_Tables
   Use CFML_Magnetic_Database
   Use CFML_gSpaceGroups

   !---- Variables ----!
   implicit none

   Type :: MAG_SYMB_TYPE
      character(len=40) :: BNS
      character(len=40) :: OG
      character(len=40) :: STD
      character(len=40) :: HallM
      character(len=15) :: ID_BNS
      character(len=15) :: ID_OG
   End Type MAG_SYMB_TYPE

   type(Mag_Symb_Type), dimension(1651) :: Mag_Symb

   character(len=50)                            :: str_BNS, str_Hall
   character(len=60), dimension(:), allocatable :: gen
   integer                                      :: n, k
   integer                                      :: ik,ngen
   real(kind=cp), dimension(3)                  :: shift

   type(spg_type)                               :: SpG

   integer                                      :: i,j,nt
   real(kind=cp) :: start, fin, st,fi
   !integer, dimension(1651)                     :: Litvin_to_IT

   !> Magnetic Symmetry Symbols (Proposed)
   call set_shubnikov_info()
   Mag_symb(:)%BNS=shubnikov_info(:)%bns
   Mag_symb(:)%HallM=shubnikov_info(:)%MHall

   !> Magnetic database from FullProf
   call Read_Magnetic_Data()
   if (err_CFML%IErr /= 0) then
      print*, trim(err_cfml%msg)
      stop
   end if

   call system("cls")
   print*,'-----------------------------------------------------'
   print*,' Testing Magnetic Symmetry Groups using Hall symbols'
   print*,'-----------------------------------------------------'

   !> Main
   call cpu_time(start)
   nt=0
   do n=1,1651
      !> Info from Database
      call cpu_time(st)
      ik=Litvin2IT(n)
      !str_BNS=adjustl(spacegroup_label_bns(n))
      
      !> Magnetic Hall symbol
      str_Hall=trim(Mag_symb(ik)%HallM)
      ngen=0
      gen=" "
      call Get_Generators(str_Hall, Gen, ngen)
      !
      !!> Constructor
      call Init_SpaceGroup(SpG)
      call Group_Constructor(gen,SpG) 
      if (Err_CFML%Ierr /= 0) then
         print*,'    --->'//trim(Err_CFML%Msg)
         cycle
      end if
      !
      !!> Identify group
      call Identify_Group(Spg)
      if(Err_CFML%Ierr == 1) then
         write(unit=*,fmt="(a)") "  WARNING: "//Err_CFML%Msg
         call clear_error()
      end if     
      if(len_trim(SpG%BNS_num) /= 0 .and. SpG%numshu /= 0 .and. len_trim(SpG%BNS_symb) == 0) then
        call set_Shubnikov_info()
        SpG%BNS_symb=Shubnikov_Info(Litvin2IT(SpG%numshu))%BNS
      end if
      call cpu_time(fi)
      !
      str_BNS=SpG%BNS_symb  !here we obtain the symbol generated by Identify_Group
      nt=nt+1
      !Litvin_to_IT(n)=ik
      if (trim(str_BNS) /= trim(Mag_symb(ik)%BNS)) then
         write(unit=*,fmt='(i6,i6,t18,a,t40,a,t60,a,f8.4,a)') nt, ik, str_BNS, Mag_symb(ik)%BNS, 'ERROR  '//shubnikov_info(ik)%STD//shubnikov_info(ik)%MHALL//'  CPU:',fi-st,"seconds"
      else
         write(unit=*,fmt='(i6,i6,t18,a,t40,a,t60,a,f8.4,a)') nt, ik, str_BNS, Mag_symb(ik)%BNS, 'OK     '//shubnikov_info(ik)%STD//shubnikov_info(ik)%MHALL//'  CPU:',fi-st,"seconds"
      end if

   end do
   call cpu_time(fin)
   write(*,"(a,f12.3,a)") "Total CPU_TIME for this calculation: ",(fin-start)/60," minutes"
   !open(unit=1,file="Litvin_to_IT.txt",action="write",status="replace")
   !write(1,"(a)")  "Litvin to International pointer "
   !write(1,"(20(i5,a))") (Litvin_to_IT(n),",",n=1,1651)
   !close(unit=1)
End Program Magnetic_Hall
